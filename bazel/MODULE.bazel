module(
    name = "envoy_toolshed",
    version = "0.3.12-dev",
)

bazel_dep(name = "aspect_bazel_lib", version = "2.22.0")
bazel_dep(name = "bazel_skylib", version = "1.8.2")
bazel_dep(name = "platforms", version = "1.0.0")
bazel_dep(name = "rules_foreign_cc", version = "0.15.1")
bazel_dep(name = "rules_pkg", version = "1.2.0")
bazel_dep(name = "rules_python", version = "1.7.0")
bazel_dep(name = "rules_shell", version = "0.6.1")
bazel_dep(name = "toolchains_llvm", version = "1.6.0", dev_dependency = True)

bazel_lib_toolchains = use_extension("@aspect_bazel_lib//lib:extensions.bzl", "toolchains")
bazel_lib_toolchains.jq(version = "1.7")
use_repo(bazel_lib_toolchains, "jq", "jq_toolchains")

PYTHON_VERSIONS = [
    "3.12",
]

python = use_extension("@rules_python//python/extensions:python.bzl", "python")

[
    python.toolchain(
        is_default = python_version == PYTHON_VERSIONS[-1],
        python_version = python_version,
    )
    for python_version in PYTHON_VERSIONS
]

pip = use_extension("@rules_python//python/extensions:pip.bzl", "pip")

[
    pip.parse(
        hub_name = "pip3",
        python_version = python_version,
        requirements_lock = "//:requirements.txt",
    )
    for python_version in PYTHON_VERSIONS
]

[
    pip.parse(
        hub_name = "website_pip3",
        python_version = python_version,
        requirements_lock = "//website:requirements.txt",
    )
    for python_version in PYTHON_VERSIONS
]

use_repo(pip, "pip3", "website_pip3")

# Load llvm_source for compile targets
http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

http_archive(
    name = "llvm_source",
    build_file_content = """filegroup(name = "all", srcs = glob(["**"]), visibility = ["//visibility:public"])""",
    sha256 = "09c08693a9afd6236f27a2ebae62cda656eba19021ef3f94d59e931d662d4856",
    strip_prefix = "llvm-project-llvmorg-18.1.8",
    url = "https://github.com/llvm/llvm-project/archive/llvmorg-18.1.8.tar.gz",
)

# Setup sysroots for LLVM toolchain using the sysroot extension
sysroot_ext = use_extension("//sysroot:extensions.bzl", "sysroot_extension")
sysroot_ext.setup()
use_repo(sysroot_ext, "sysroot_linux_amd64", "sysroot_linux_arm64")

# Configure LLVM toolchain
llvm = use_extension("@toolchains_llvm//toolchain/extensions:llvm.bzl", "llvm", dev_dependency=True)
llvm.toolchain(
    name = "llvm_toolchain",
    llvm_version = "18.1.8",
)
llvm.sysroot(
    name = "llvm_toolchain",
    label = "@sysroot_linux_amd64//:sysroot",
    targets = ["linux-x86_64"],
)
llvm.sysroot(
    name = "llvm_toolchain",
    label = "@sysroot_linux_arm64//:sysroot",
    targets = ["linux-aarch64"],
)
use_repo(llvm, "llvm_toolchain")

register_toolchains("@llvm_toolchain//:all", dev_dependency = True)
